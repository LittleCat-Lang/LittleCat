/***
    Network module
    From: 0.1.2
    Minimal version: 0.1.2
**/
method parseUrl(url: string): URL {
    urlRegex = regexMatch("^(([^:/?#]+):)?(//([^/?#]*))?([^?#]*)(\\?([^#]*))?(#(.*))?", url)
    return :URL(urlRegex[2], urlRegex[4], urlRegex[5], urlRegex[7], urlRegex[9])
}

class URL(scheme  : string,
            host  : ?string,
            path  : ?string,
            query : ?string,
            hash  : ?string) {
    if !path.endsWith("/"): path += "/"

    method openConnection(): ?Connection {
        hostParts = []
        if host!=null: hostParts = host.split(":")

        if scheme=="http": {
            return :HTTPConnection(:Socket(hostParts[0] ?: host, :int(hostParts[1] ?: 80)), this)
        }else if scheme=="https": {
            return :HTTPConnection(:SSLSocket(hostParts[0] ?: host, :int(hostParts[1] ?: 443)), this)
        }else if scheme=="file": {
            return :FileConnection(:File(path))
        }else printError("Current scheme not supported")

        return null
    }

    method getFullPath(): string {
        fullPath = ""
        if path!=null: fullPath += path
        if query!=null: fullPath += "?" + query
        if hash!=null: fullPath += "#" + hash

        return fullPath
    }

    method toString(): string {
        url = scheme + "://"
        if host!=null: url += host

        return url + getFullPath()
    }
}

class HTTPConnection(socket: Socket, url: URL) extends Connection {
    methodType = "GET"
    isConnected = false
    headers = [
        "Host: "+url.host
    ]

    method getOutput(): Output {
        connect()
        return socket.getOutput()
    }

    method getInput(): Input {
        connect()
        return socket.getInput()
    }

    method connect() {
        if isConnected: return;

        isConnected = true
        if socket is SSLSocket: {
            socket.startHandshake()
        }

        output = socket.getOutput()
        output.println(methodType+" "+url.getFullPath()+" HTTP/1.1")
        headers.forEach(-> (header) output.print(header+"\r\n"))
        output.print("\r\n")
        output.flush()
    }
}

class FileConnection(file: File) extends Connection {
    method getOutput(): Output = file.openOutput()
    method getInput(): Input = file.openInput()
}

class Connection {
    method getOutput(): Output {
        printError("this is abstract class xD")
    }

    method getInput(): Input {
        printError("this is abstract class xD")
    }
}