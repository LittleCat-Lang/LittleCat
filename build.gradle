plugins {
    id 'java-library'
    id 'antlr'
    id 'org.jetbrains.kotlin.jvm' version '1.5.21'
    id "maven-publish"
    id "signing"
}

sourceSets.main.java.srcDirs 'src/main/gen'

group 'lclang'
version '0.1'

repositories {
    mavenCentral()
}

dependencies {
    antlr 'org.antlr:antlr4:4.9.2'
    implementation 'commons-cli:commons-cli:1.4'
    implementation 'com.github.albfernandez:juniversalchardet:2.4.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.5.21'

    testImplementation(platform('org.junit:junit-bom:5.7.2'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

jar {
    manifest.attributes "Main-Class": "lclang.MainKt"

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        outputs.upToDateWhen {false}
        showStandardStreams = true
        showExceptions = true
        showCauses = true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "lclang"
            artifactId = project.archivesBaseName
            version = project.version

            pom {
                name = 'Little Cat'
                description = ''
                url = 'https://github.com/lclang/LittleCat'

                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }

                developers {
                    developer {
                        id = 'levkopo'
                        name = 'Lev Koporushkin'
                        email = 'levkopoag@gmail.com'
                    }

                    developer {
                        id = 'VitiaCat'
                        name = 'Vitia Kulesh'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/lclang/LittleCat.git'
                    developerConnection = 'scm:git:ssh://github.com/lclang/LittleCat.git'
                    url = 'https://github.com/lclang/LittleCat'
                }
            }

            from components.java
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/lclang/LittleCat")
            credentials {
                username = project.findProperty('githubUsername') ?: System.getenv('githubUsername')
                password = project.findProperty('githubKey') ?: System.getenv('githubKey')
            }
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

signing {
    sign publishing.publications.mavenJava
}